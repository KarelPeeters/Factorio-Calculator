import groovy.json.JsonSlurper

import java.nio.file.Paths

ext {
    //Factorio install location
    def properties = new Properties()
    properties.load(file("local.properties").newInputStream())
    factorioPath = properties.getProperty("factorioPath")
    factorioModPath = Paths.get(factorioPath, "mods").toString()

    //Mod name and version
    def info = new JsonSlurper().parse(file('src/info.json'))
    modName = info.name
    modVersion = info.version
    modArchiveName = "${modName}_${modVersion}".toString()
}

task build(type: Zip) {
    from("lib") { into "lib" }
    from "src"

    into modArchiveName
    baseName modArchiveName
    extension "zip"
    destinationDir(file("build/"))
}

task clean(type: Delete) {
    delete fileTree(dir: "build", include: "*")
}

task uninstall(type: Delete) {
    delete fileTree(dir: factorioModPath, include: "${modName}_*.zip")
}

task install(type: Copy) {
    dependsOn build, uninstall

    from "build/"
    into factorioModPath
    include "${modArchiveName}.zip"
}