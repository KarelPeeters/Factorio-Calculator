import groovy.json.JsonSlurper

import java.nio.file.Paths

ext {
    //Factorio install location
    def properties = new Properties()
    properties.load(file("local.properties").newInputStream())
    factorioPath = properties.getProperty("factorioPath")
    factorioModPath = Paths.get(factorioPath, "mods").toString()

    //Mod name and version
    def info = new JsonSlurper().parse(file('src/info.json'))
    modName = info.name
    modVersion = info.version
    modArchiveName = "${modName}_${modVersion}".toString()
}

task assemble(type: Zip) {
    from("lib") { into "lib" }
    from "src"

    into modArchiveName
    baseName modArchiveName
    destinationDir(buildDir)
}

task build {
    dependsOn assemble
}

task clean(type: Delete) {
    delete fileTree(dir: buildDir, include: "*")
}

task uninstall(type: Delete) {
    fileTree(dir: factorioModPath, include: "/${modName}*").visit { FileVisitDetails details  ->
        delete details.getFile()
    }
}

task install(type: Copy) {
    dependsOn assemble, uninstall

    from zipTree(Paths.get(buildDir.path, "${modArchiveName}.zip"))
    into factorioModPath
}